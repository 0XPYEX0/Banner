buildscript {
	repositories {
		maven {
			name = 'mohist'
			url = 'https://maven.mohistmc.com/'
		}
	}
	dependencies {
		classpath 'com.mohistmc.banner:banner-gradle-plugin:1.0.1'
	}
}

plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id 'org.ajoberstar.grgit' version '5.2.0'
}

apply plugin: 'com.mohistmc.banner'

banner {
	mcVersion = project.minecraft_version
	bukkitVersion = 'v1_20_R3'
}

version = project.minecraft_version + "-" + ci_version()
group = project.maven_group

configurations {
	extraLibs
	removeLibs
	metest
}

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = 'spigot'
		url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
	}
	maven {
		name = 'mohist'
		url = 'https://maven.mohistmc.com/'
	}
	maven {
		name = 'izzel'
		url = 'https://maven.izzel.io/releases'
	}
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = 'JitPack'
		url = 'https://jitpack.io'
	}
	maven {
		name = "Sponge"
		url = 'https://repo.spongepowered.org/maven'
	}
	maven {
		name = "Adventure"
		url = 'https://jd.advntr.dev/'
	}
	mavenCentral()
}

dependencies {
	implementation 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Make a collection of all api modules we wish to use
	Set<String> apiModules = [
			"fabric-api-base",
			"fabric-events-interaction-v0",
			"fabric-entity-events-v1",
			"fabric-lifecycle-events-v1",
			"fabric-transitive-access-wideners-v1"
	]

	// Add each module as a dependency
	apiModules.forEach {
		modImplementation(fabricApi.module(it, "${project.fabric_version}"))
	}

	compileOnly "org.jetbrains:annotations:21.0.1"

	// bukkit
	implementation(include("org.yaml:snakeyaml:2.0"))
	implementation(include("com.mohistmc:i18n:0.6"))
	implementation(include('com.mohistmc:json:0.2'))
	implementation(include('com.mohistmc:tools:0.2'))
	implementation(include('com.mohistmc:dynamicenum:0.2'))
	implementation(include('javax.inject:javax.inject:1'))
	// jansi support
	implementation(include("org.jline:jline-terminal:3.21.0"))
	implementation(include("org.jline:jline-terminal-jansi:3.21.0"))
	implementation(include("net.minecrell:terminalconsoleappender:1.4.0"))

	// craftbukkit
	implementation "org.fusesource.jansi:jansi:1.18"
	implementation "jline:jline:2.14.6"
	implementation "com.googlecode.json-simple:json-simple:1.1.1"
	implementation "org.xerial:sqlite-jdbc:3.42.0.0"
	implementation "com.mysql:mysql-connector-j:8.0.33"

	implementation "net.md-5:SpecialSource:1.11.2"

	// spigot
	implementation "net.md-5:bungeecord-chat:1.16-R0.4"
	implementation "io.izzel:tools:1.3.0"

	implementation(include("org.apache.logging.log4j:log4j-iostreams:2.20.0"))

	//class modify
	modImplementation(include("com.github.wdog5:mixin-tools:1.0.1"))
	annotationProcessor 'com.github.wdog5:mixin-tools:1.0.1'

	// mappings remapper
	implementation(include("net.fabricmc:mapping-io:0.3.0"))

	// mohist lib
	implementation(include("com.mohistmc:librariesvault:1.20.2"))

	compileOnly fileTree(dir: 'libs', includes: ["*.jar"])
	extraLibs fileTree(dir: "libs", includes: ["*.jar"])
	configurations.api.extendsFrom( (configurations.extraLibs) )
}

loom {
	accessWidenerPath = file("src/main/resources/banner.accesswidener")
	log4jConfigs.from(file("src/main/resources/log4j2_banner.xml"))
	serverOnlyMinecraftJar()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}


java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-Xlint:-dep-ann' << '-Xlint:-removal'
}

jar {
	from("LICENSE")
	from {
		(configurations.extraLibs - configurations.removeLibs).filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
	}
	manifest {
		attributes(
				'Main-Class': 'com.mohistmc.banner.BannerGUI',
				'Specification-Title'   : 'Banner',
				'Specification-Vendor'  : 'MohistMC',
				'Specification-Version' : ci_version(),
				'Implementation-Title'  : 'Banner',
				'Implementation-Version': version,
				'Implementation-Vendor' : 'MohistMC'
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {

	}
}

import groovy.json.JsonSlurper
static String ci_version() {
	try {
		def conn = new URL("https://ci.codemc.io/job/MohistMC/job/Banner-1.20.4/api/json").openConnection()
		conn.setRequestProperty("User-Agent", "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/112.0.0.0 Safari/537.36")
		conn.connect()
		def root = conn.content.text
		def jsonSluper = new JsonSlurper()

		String data = jsonSluper.parseText(root)
		def number = data.substring(data.indexOf("number")).split(",")
		return Integer.valueOf(number[0].replace("number=", "")).intValue()
	} catch (Exception ignored) {
		return "1.20.4"
	}
}

remapSpigotJar {
	includes.add('net/minecraft/world/level/block/ChestBlock$DoubleInventory')
	includes.add('net/minecraft/world/level/block/entity/LecternBlockEntity$LecternInventory')
	includes.add('net/minecraft/world/level/block/ChestBlock$DoubleInventory')
	includes.add('net/minecraft/world/level/block/ChestBlock$2$1')
	includes.add('net/minecraft/world/level/block/entity/LecternBlockEntity$1')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer$1.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer$2.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer$3.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer$4.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/CraftServer$5.class')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/command/ColouredConsoleSender')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/util/ServerShutdownThread')
	excludes.add('org/bukkit/craftbukkit/v1_20_R3/util/TerminalConsoleWriterThread')
	excludesApi.add('org/bukkit/event/block/BlockCookEvent.class')
	excludesApi.add('org/bukkit/event/Event$Result.class')
	excludesApi.add('org/bukkit/plugin/java/JavaPluginLoader$1.class')
	excludesApi.add('org/bukkit/plugin/java/LibraryLoader$1.class')
	excludesApi.add('org/bukkit/event/inventory/FurnaceSmeltEvent.class')
}
